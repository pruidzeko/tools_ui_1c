&НаКлиенте
Процедура ВыполнитьОтладку(Команда)
	ТекДанные=Элементы.ТаблицаСохраненныхНастроек.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	УИ_ОбщегоНазначенияКлиент.ЗапуститьКонсольОтладкиПоКлючуНастройкиДанныхОтладки(ТекДанные.КлючНастроек);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицу(Команда)
	ОбновитьТаблицуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуНаСервере()
	ТаблицаСохраненныхНастроек.Очистить();

	СтруктураПоиска=Новый Структура;
	СтруктураПоиска.Вставить("КлючОбъекта", КлючОбъектаНастроек);

	Выборка=ХранилищеСистемныхНастроек.Выбрать(СтруктураПоиска);

	Пока Выборка.Следующий() Цикл
		НоваяСтрока=ТаблицаСохраненныхНастроек.Добавить();
		НоваяСтрока.КлючНастроек=Выборка.КлючНастроек;
		НоваяСтрока.Пользователь=Выборка.Пользователь;

		МассивКлючаНастроек=СтрРазделить(НоваяСтрока.КлючНастроек, "/");

		НоваяСтрока.Автор=МассивКлючаНастроек[1];
		НоваяСтрока.ТипОбъектаОтладки=МассивКлючаНастроек[0];
		Попытка
			НоваяСтрока.ДатаСоздания=Дата(МассивКлючаНастроек[2]);
		Исключение
			НоваяСтрока.ДатаСоздания="";
		КонецПопытки;

	КонецЦикла;

	ТаблицаСохраненныхНастроек.Сортировать("ДатаСоздания УБЫВ");
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	ВыделенныеСтроки=Элементы.ТаблицаСохраненныхНастроек.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	УдалитьНаСервере(Элементы.ТаблицаСохраненныхНастроек.ТекущаяСтрока);
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервере(ТекущаяСтрока)
	СтрокаТЧ=ТаблицаСохраненныхНастроек.НайтиПоИдентификатору(ТекущаяСтрока);

	УИ_ОбщегоНазначения.ХранилищеСистемныхНастроекУдалить(КлючОбъектаНастроек, СтрокаТЧ.КлючНастроек,
		СтрокаТЧ.Пользователь);

	ОбновитьТаблицуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КлючОбъектаНастроек=УИ_ОбщегоНазначенияКлиентСервер.КлючДанныхОбъектаДанныхОтладкиВХранилищеНастроек();

	ОбновитьТаблицуНаСервере();
КонецПроцедуры